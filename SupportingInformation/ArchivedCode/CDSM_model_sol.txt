model{ 
  
  ##Priors
  
  for(j in 1:nsites){
    
    #Abundance prior
    alpha[j] ~ dnorm(0, 0.01)
    
    #Detection prior
    sigma[j] ~ dunif(0, 500)
    
  }#End j loop
  
  #Group size prior
  beta ~ dunif(0, 50)
  
  ##Likelihood
  
  #Multinomial detection component
  for(i in 1:nobs){
    
    dclass[i] ~ dcat(fct[1:nG, site[i]])
    
  }#End i loop
  
  for(j in 1:nsites){
    
    f.0[j] <- 2 * dnorm(0, 0, 1/sigma[j]^2)
    
    #Construct cell probabilities for nG cells
    for(k in 1:nG){  
      
      up[k,j]<-pnorm(db[k+1], 0, 1/sigma[j]^2) 
      low[k,j]<-pnorm(db[k], 0, 1/sigma[j]^2) 
      p[k,j]<- 2 * ( up[k,j] - low[k,j])
      f[k,j]<- p[k,j]/f.0[j]/v                          
      fc[k,j]<- f[k,j] * pi[k]       
      fct[k,j]<-fc[k,j]/sum(fc[1:nG,j]) 
      
    }#End k loop
    
    #Detection probability at each site (sum of rectangles)
    pcap[j] <- sum(fc[1:nG,j])               
    
    #Observation process
    y[j] ~ dbin(pcap[j], N[j])
    
    #Description of latent number of groups
    N[j] ~ dpois(lambda[j])
    
    #Linear model for number of groups
    lambda[j] <- exp(alpha[j] + log(offset[j]))
    
    #Group size
    gs.lam[j] <- exp(beta)
    
  }#End j loop
  
  for(i in 1:nobs){
    
    gs[i] ~ dpois(gs.lam[site[i]]) T(1,)
    
  }#End i loop
  
  ##Derived quantities
  
  #Number of groups within
  Nin <- sum(N[1:nsites])
  
  for(j in 1:nsites){
    
    #Abundance within
    Ntotal[j] <- N[j] * gs.lam[j]
    
  } #End j loop
  
  Nintotal <- sum(Ntotal[])
  
  #Density
  D <- (939.316/164.4837)
  
  #Number of total groups in sampling boundary
  Nreal <- Nin * D
  
  #Abundance in sampling boundary
  Nrealtotal <- Nintotal * D
  
}